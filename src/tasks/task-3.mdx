---
title: "Task 3"
date: "2024-06-12"
description: "This is the third task."
---

import Tabs from '../components/Tabs'

# Task 3

<Tabs>
  <Tab label="AWS EC2">
    making aws ecs using terraform

    ![Task 1 Code](/task1Code.png)
    ![Task 1 EC2](/task1EC2.png)

    Created EC2 instance using AWS.
  </Tab>
  <Tab label="AWS VPC">
    # making aws vpc using terraform

    ![Task 2 Code](/task2code.png)
    ![Task 2 running](/task2running.png)
    ![Task 2 result](/task2result.png)


    # Code
    ```tf
    provider "aws" {
      region     = "us-east-1"
      secret_key = "*********************"
      access_key = "***********"
    }
    data "aws_ami" "ubuntu" {
      most_recent = true
      filter {
        name   = "name"
        values = ["ubuntu/images/hvm-ssd/ubuntu-jammy-22.04-amd64-server-*"]
      }
      filter {
        name   = "virtualization-type"
        values = ["hvm"]
      }
      owners = ["099720109477"]
    }
    resource "aws_instance" "Sample_demo" {
      count                       = 1
      ami                         = data.aws_ami.ubuntu.id
      instance_type               = "t2.micro"
      key_name                    = var.key_name                      #this enables SSH using your key
      vpc_security_group_ids      = [aws_security_group.allow_tls.id] #attach the security group
      associate_public_ip_address = true
      tags = {
        Name = "EC2_Without_AMI"
      }
    }
    #use the default VPC
    data "aws_vpc" "default" {
      default = true
    }
    resource "aws_security_group" "allow_tls" {
      name        = "allow_tls"
      description = "Allow TLS inbound traffic and all outbound traffic"
      vpc_id      = data.aws_vpc.default.id
      tags = {
        Name = "allow_tls"
      }
    }
    # Ingress Rule for IPv4 - HTTPS traffic within the VPC
    resource "aws_vpc_security_group_ingress_rule" "allow_tls_ipv4" {
      security_group_id = aws_security_group.allow_tls.id
      cidr_ipv4         = data.aws_vpc.default.cidr_block #If public HTTPS access is required then use "0.0.0.0/0"
      from_port         = 443
      ip_protocol       = "tcp"
      to_port           = 443
    }
    # Ingress Rule for IPv6 - HTTPS traffic within the VPC
    resource "aws_vpc_security_group_ingress_rule" "allow_tls_ipv6" {
      security_group_id = aws_security_group.allow_tls.id
      cidr_ipv6         = "::/0"
      from_port         = 443
      ip_protocol       = "tcp"
      to_port           = 443
    }
    # Egress Rule - Allow all outbound IPv4 traffic
    resource "aws_vpc_security_group_egress_rule" "allow_all_traffic_ipv4" {
      security_group_id = aws_security_group.allow_tls.id
      cidr_ipv4         = "0.0.0.0/0"
      ip_protocol       = "-1" # semantically equivalent to all ports
    }
    resource "aws_vpc_security_group_egress_rule" "allow_all_traffic_ipv6" {
      security_group_id = aws_security_group.allow_tls.id
      cidr_ipv6         = "::/0"
      ip_protocol       = "-1" # semantically equivalent to all ports
    }
    # Ingress Rule for SSH Access (Port 22)
    resource "aws_vpc_security_group_ingress_rule" "allow_ssh_ipv4" {
      security_group_id = aws_security_group.allow_tls.id
      cidr_ipv4         = "0.0.0.0/0" # Allow SSH from anywhere (consider restricting forproduction)
      from_port         = 22
      ip_protocol       = "tcp"
      to_port           = 22
    }
    # Optional: Variable for Key Pair (to SSH into the EC2 instance)
    variable "key_name" {
      description = "Name of the AWS key pair for SSH access"
      type        = string
      default     = "devkey" # Replace with your actual key pair name
    }
    # Note: To use SSH, update the aws_instance block to include:
    # key_name = var.key_name
    # security_groups = [aws_security_group.allow_tls.name]
    ```
  </Tab>
  <Tab label="Cloud Watch">
    # AWS CloudWatch
    ![cloud watch](/cloudwatch.png)
    ![code](/cloudwatchCode.png)

    ``` tf
      resource "aws_cloudwatch_metric_alarm" "custom_metric_alarm" {
        alarm_name          = "MyCustomMetric_Alarm"
        alarm_description   = "Alarm when MyCustomMetric is greater than 10"
        comparison_operator = "GreaterThanThreshold"
        evaluation_periods  = 1
        metric_name         = "MyCustomMetric"
        namespace           = "MyCustomNamespace"
        period              = 60
        statistic           = "Average"
        threshold           = 10
        actions_enabled     = false

        dimensions = {
          InstanceId = "i-mockinstance"
        }
      }
    ```
  </Tab>
  <Tab label="Cloud Trail">
    # AWS Cloud Trail
    ![cloud trail](/cloudTrail.png)
    ![code](/cloudtrailcode.png)

    ``` tf
      # Create S3 bucket for CloudTrail logs
      resource "aws_s3_bucket" "cloudtrail_bucket" {
        bucket = "cloudtrail-logs-${random_id.suffix.hex}"
        force_destroy = true
      }

      # Generate a random suffix to ensure unique bucket name
      resource "random_id" "suffix" {
        byte_length = 4
      }

      # Bucket policy to allow CloudTrail to write logs
      resource "aws_s3_bucket_policy" "cloudtrail_policy" {
        bucket = aws_s3_bucket.cloudtrail_bucket.id

        policy = jsonencode({
          Version = "2012-10-17",
          Statement = [
            {
              Effect = "Allow",
              Principal = {
                Service = "cloudtrail.amazonaws.com"
              },
              Action = "s3:PutObject",
              Resource = "${aws_s3_bucket.cloudtrail_bucket.arn}/AWSLogs/${data.aws_caller_identity.current.account_id}/*",
              Condition = {
                StringEquals = {
                  "s3:x-amz-acl" = "bucket-owner-full-control"
                }
              }
            },
            {
              Effect = "Allow",
              Principal = {
                Service = "cloudtrail.amazonaws.com"
              },
              Action = "s3:GetBucketAcl",
              Resource = aws_s3_bucket.cloudtrail_bucket.arn
            }
          ]
        })
      }

      # Get current account ID for bucket policy
      data "aws_caller_identity" "current" {}

      # Create the CloudTrail trail
      resource "aws_cloudtrail" "main" {
        name                          = "cloudtrail-example"
        s3_bucket_name                = aws_s3_bucket.cloudtrail_bucket.id
        include_global_service_events = true
        is_multi_region_trail         = true
        enable_log_file_validation    = true
      #   is_logging                    = true
      }
    ```
  </Tab>
  <Tab label="AWS Lambda">
    # AWS Lambda
    ![lambda](/lambda.png)
    ![lambda2](/lambdacode.png)

    ```
resource "aws_iam_role" "lambda_exec_role" {
  name = "lambda_exec_role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [{
      Action = "sts:AssumeRole",
      Principal = {
        Service = "lambda.amazonaws.com"
      },
      Effect = "Allow",
      Sid    = ""
    }]
  })
}

resource "aws_iam_role_policy_attachment" "lambda_basic" {
  role       = aws_iam_role.lambda_exec_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
}

resource "aws_lambda_function" "hello_lambda" {
  function_name = "hello_lambda"
  role          = aws_iam_role.lambda_exec_role.arn
  runtime       = "python3.9"
  handler       = "lambda_function.lambda_handler"
  filename      = "lambda_function_payload.zip"

  source_code_hash = filebase64sha256("lambda_function_payload.zip")
}
    ```
  </Tab>
  <Tab label="Cloud Formation">
  # Cloud Formation
  ![cf](/cloudFormation.png)
  ![instance](/CFinstance.png)
  # code 
  ``` tf
provider "aws" {

  region     = "eu-west-1"

}
# Pass KeyPair name as a variable
variable "cloudkey" {
  description = "EC2 Key Pair name"
  type        = string
}

resource "aws_cloudformation_stack" "ec2_stack" {
  name          = "ec2-cfn-stack"
  template_body = file("${path.module}/template.yaml")

  parameters = {
    KeyName      = var.cloudkey
    InstanceType = "t2.micro"
  }

  capabilities = ["CAPABILITY_NAMED_IAM"]
}
  ```

  # yaml file
  ```
  AWSTemplateFormatVersion: '2010-09-09'
Description: Launch EC2 instance

Parameters:
  KeyName:
    Type: String
    Description: Name of an existing EC2 KeyPair
  InstanceType:
    Type: String
    Default: t2.micro
    AllowedValues:
      - t2.micro
      - t2.small
      - t3.micro
    Description: EC2 instance type

Resources:
  EC2Instance:
    Type: AWS::EC2::Instance
    Properties:
      InstanceType: !Ref InstanceType
      KeyName: !Ref KeyName
      ImageId: ami-0b3e7dd7b2a99b08d  # Amazon Linux 2 (update for your region)
      Tags:
        - Key: Name
          Value: CFN-EC2-From-Terraform
  ```
  </Tab>
  <Tab label="S3 website">

  ## Launch a Static Website on S3

  ![a](/Task4cr.png)
  ![b](/Task4Runnning.png)
  ![c](/Task4s3.png)

  ## Code

  ### main.tf
  ```tf
  provider "aws" {
      region = "ap-south-1"
      access_key ="***"
      secret_key ="***"
  }

  resource "aws_s3_bucket" "website" {
    bucket = "bytewave-website-xx"  # replace 'xx' with your initials
  }

  resource "aws_s3_bucket_website_configuration" "site" {
    bucket = aws_s3_bucket.website.id

    index_document {
      suffix = "index.html"
    }
  }

  resource "aws_s3_bucket_public_access_block" "block" {
    bucket = aws_s3_bucket.website.id

    block_public_acls       = false
    block_public_policy     = false
    ignore_public_acls      = false
    restrict_public_buckets = false
  }

  resource "aws_s3_bucket_policy" "policy" {
    bucket = aws_s3_bucket.website.id

    policy = jsonencode({
      Version = "2012-10-17",
      Statement = [{
        Sid       = "PublicReadGetObject",
        Effect    = "Allow",
        Principal = "*",
        Action    = "s3:GetObject",
        Resource  = "${aws_s3_bucket.website.arn}/*"
      }]
    })
  }

  resource "aws_s3_object" "index" {
    bucket       = aws_s3_bucket.website.id
    key          = "index.html"
    source       = "${path.module}/index.html"
    content_type = "text/html"
    # Remove the ACL line below
    # acl          = "public-read"
  }
  ```
  ### output.tf
  ```tf
  output "website_url" {
    value = aws_s3_bucket_website_configuration.site.website_endpoint
  }
  ```
  ### index.html
  ```html
  <html>
    <head><title>ByteWave Solutions</title></head>
    <body><h1>Welcome to ByteWave Solutions</h1></body>
  </html>
  ```

  </Tab>
    <Tab label="EBS">
    <h3>EBS</h3>
    ```hcl
    resource "aws_ebs_volume" "task3_volume" {
      availability_zone = aws_instance.task3_instance.availability_zone
      size              = 1
      type              = "gp2"

      tags = {
        Name = "Task3-EBS-Volume-FreeTier"
      }
    }

    # Attach EBS Volume to EC2
    resource "aws_volume_attachment" "task3_attachment" {
      device_name = "/dev/sdf"
      volume_id   = aws_ebs_volume.task3_volume.id
      instance_id = aws_instance.task3_instance.id
      force_detach = true
    }

    # SSH Key Variable
    variable "key_name" {
      description = "SSH key name for EC2"
      type        = string
      default     = "cloudkey" # Replace with your actual key pair name
    }
    ```
    <img src="/ebs.png" alt="EBS" />
  </Tab>
  <Tab label="RDS">
    <h3>RDS</h3>
    ```hcl
    resource "aws_db_instance" "task3_rds" {
      allocated_storage    = 20
      storage_type         = "gp2"
      engine               = "mysql"
      engine_version       = "8.0.35"
      instance_class       = "db.t3.micro"
      identifier           = "task3-mysql-instance"
      username             = var.db_username
      password             = var.db_password
      db_name              = "task3db"
      publicly_accessible  = true
      skip_final_snapshot  = true

      tags = {
        Name = "Task3-RDS-FreeTier"
      }
    }
    ```
    <img src="/RDS (2).png" alt="RDS" />
  </Tab>
  <Tab label="Athena">
    <h3>Athena</h3>
    ```hcl
    # Create an S3 bucket for Athena query results
    resource "aws_s3_bucket" "athena_results_bucket" {
      bucket = "sreedevi-athena-results-bucket"
      force_destroy = true
    }

    # Create an Athena workgroup (optional but best practice)
    resource "aws_athena_workgroup" "task3_workgroup" {
      name = "task3_workgroup"

      configuration {
        result_configuration {
          output_location = "s3://${aws_s3_bucket.athena_results_bucket.bucket}/query-results/"
        }
      }
    }

    # Athena database
    resource "aws_athena_database" "task3_db" {
      name   = "task3db"
      bucket = aws_s3_bucket.athena_results_bucket.id
    }

    # Athena table using a sample CSV layout
    resource "aws_athena_named_query" "task3_create_table" {
      name      = "create_sample_table"
      database  = aws_athena_database.task3_db.name
      workgroup = aws_athena_workgroup.task3_workgroup.name
      query     = <<EOF
    CREATE EXTERNAL TABLE IF NOT EXISTS sample_table (
      name string,
      age int,
      city string
    )
    ROW FORMAT SERDE 'org.apache.hadoop.hive.serde2.lazy.LazySimpleSerDe'
    WITH SERDEPROPERTIES (
      'serialization.format' = ','
    )
    LOCATION 's3://${aws_s3_bucket.athena_results_bucket.bucket}/sample-data/'
    TBLPROPERTIES ('has_encrypted_data'='false');
    EOF
    }
    ```
    <img src="/athena.png" alt="Athena" />
  </Tab>

  <Tab label="CDK">
    <h3>CDK</h3>
        ```
    import { Construct } from "constructs";
import { App, TerraformStack } from "cdktf";
import { AwsProvider } from "@cdktf/provider-aws/lib/provider";
import { S3Bucket } from "@cdktf/provider-aws/lib/s3-bucket";

class MyS3Stack extends TerraformStack {
  constructor(scope: Construct, id: string) {
    super(scope, id);

    new AwsProvider(this, "AWS", {
      region: "eu-west-1",
    });

    new S3Bucket(this, "MyBucket", {
      bucket: "cdktf-demo-s3-bucket-unique-1234", // ✅ Must be globally unique!
    });
  }
}

const app = new App();
new MyS3Stack(app, "s3-stack");
app.synth();

    ```
    ![image](/S3CDK.png)
    # s3 bucket created with cdk
    ![image](/Cdk1.png)
    # Deployment of CDK
    ![image](/Cdk2.png)
    # Code of CDK
    ![image](/cdkcode.png)
    
 
  </Tab>
  
</Tabs>